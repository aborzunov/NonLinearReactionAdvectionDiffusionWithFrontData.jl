#' ## Решение прямой задачи для генерации априорной информации
#' Сопряженная задача определяется для сеточного решения некоторой прямой задачи,
#' и некоторых априорных данных. Поэтому, сначала необходимо сгенерировать
#' априорные данные и некоторую матрицу `u`.
#' Сделаем это на увеличенном числе интервалов, т.к. решение сопряженной задачи
#' менее гладкое.
using NonLinearReactionAdvectionDiffusionWithFrontData

u_l(t) = -8
u_r(t) =  4
q(x) = 4*sin(3 * π * x);        # Коэффициент линейного усиления, который в обратной
                                # задаче необходимо определить, но при генерации априорной
                                # информации мы задаем некоторый коэффициент, который,
                                # собственно, после имея априорную информацию и будем определять.
ε = 0.2;                        # Малый параметр при старшей производной
a, b = 0, 1;                    # Область по X
t₀, T = 0, 0.28;                # Область по T
N, M = 50, 80;                  # Увеличенное Кол-во разбиений по X, T
h = (b-a)/N;                    # шаг по X
τ = (T-t₀)/M;                   # шаг по T
Xₙ = [a  + n*h for n in 0:N];   # Сетка по Х
Tₘ = [t₀ + m*τ for m in 0:M];   # Сетка по Т
qₙ =      q.(Xₙ);               # Сеточные значения коэффициента линейного усиления
ulₘ=    u_l.(Tₘ);               # Сеточные значения левого  ГУ
urₘ=    u_r.(Tₘ);               # Сеточные значения правого ГУ
y₀ = u_init.(Xₙ);               # Начальные условия
#
u, XX, TP = solve(y₀, Xₙ, N, Tₘ, M, ε, ulₘ, urₘ, qₙ);
nothing #hide

#' ## Генерация априорной информации
ϕl = phidetermination(qₙ, ulₘ, Xₙ, N, Tₘ, M);                               # Левый вырожденный корень
ϕr = phidetermination(reverse(qₙ), urₘ, reverse(Xₙ), N, Tₘ, M);             # Нужно подать инвертированную сетку
ϕr = reverse(ϕr, dims=1);                                                   # А после — инвертировать решение по X
ϕ = NonLinearReactionAdvectionDiffusionWithFrontData.Φ(ϕl, ϕr, N, M);       # Серединный корень
f1 = NonLinearReactionAdvectionDiffusionWithFrontData.f1(ϕ, u, Xₙ, N, M);   # Положение переходного слоя
f2 = NonLinearReactionAdvectionDiffusionWithFrontData.f2(f1, u, Xₙ, N, M);  # Значение функции на переходном слое
nothing #hide

#' ## Решение сопряженной задачи

Uₙₘ = u;                        # Сохраним старую матрицу
y₀ = [0.0 for i in 1:N+1];      # Нулевые начальные условия
ψl = [0.0 for i in 1:M+1];      # Нулевые ГУ
ψr = [0.0 for i in 1:M+1];      # Нулевые ГУ

ψ = solve_adjoint(y₀, Xₙ, N, Tₘ, M, ε, ψl, ψr, qₙ, Uₙₘ, f1, f2, w = 0.0005)
nothing #hide

#' ## Визуализация

# На отрисовку, решение сопряженной задачи передадим в инвертированном времени.
# Передадим свою подпись к графиками с помощью keyword `label="\\psi"`,
# не забыл про экранировку спец символа
# Нарисует гиф с неравномерной скоростью по оси T, первые 80 шагов
# отрисуем полностью, а из последующих выберем каждый десятый, с
# помощью keyword `frames_to_write = [1:80; 81:10:M+1]
make_gif(ψ[:,end:-1:1], XX, Tₘ[end:-1:1]; frames_to_write=[1:80; 81:10:M+1], label="\\psi",
         name="adjoint_example.gif", convert2mp4 = true)

#' Результат должен быть около нулевой, ведь в качестве текущего приближения `q` мы взяли искомое,
#' а при нем — градиент должен обнуляться.
